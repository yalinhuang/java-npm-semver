plugins {
    id 'com.jfrog.bintray' version '1.8.4'
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 1.8
group = 'com.github.yalinhuang'
version = System.getProperty('pkgVersion')
description = 'SemVer implementation that matches npmâ€™s'
def githubURL = 'https://github.com/yalinhuang/java-npm-semver'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        mavenLocal()
    }

    publications {
        mavenPublication(MavenPublication) {
            from components.java

            artifactId project.name

            pom {
                name = "${project.group}:${project.name}"
                description = project.description
                url = githubURL

                developers {
                    developer {
                        id = 'yalinhuang'
                        name = 'Ya-Lin Huang'
                        email = 'huangyl@gmail.com'
                    }
                }
                scm {
                    url = githubURL
                }
                licenses {
                    license {
                        name ='GNU Lesser General Public License, version 2.1'
                        url = 'https://www.gnu.org/licenses/lgpl-2.1.html'
                        distribution ='repo'
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    publish = true

    pkg {
        repo = 'lgpl-candidates'
        name = project.name
        licenses = ['LGPL-2.1']
        vcsUrl = githubURL
    }

    version {
        name = project.version
        released = new Date()
        gpg {
            sign = true
            passphrase = System.getProperty('bintray.gpgpassphrase')
        }
        /*
        mavenCentralSync {
            sync = true
            user = System.getProperty('sonatype.user')
            password = System.getProperty('sonatype.password')
        }
         */
    }
}