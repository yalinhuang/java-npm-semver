plugins {
    id 'nebula.maven-publish' version '14.1.1'
    id 'nebula.javadoc-jar' version '14.1.1'
    id 'nebula.source-jar' version '14.1.1'
    id 'nebula.info' version '5.2.0'
    id 'nebula.contacts' version '5.1.0'
    id 'nebula.release' version '13.0.0'
}

apply plugin: 'java'
apply plugin: 'signing'

sourceCompatibility = 1.8
group = 'com.github.yalinhuang'
description = 'SemVer implementation that matches npmâ€™s'

contacts {
    'huangyl@gmail.com' {
        moniker 'Ya-Lin Huang'
        github 'yalinhuang'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

signing {
    required {
        if (gradle.taskGraph.hasTask(":publishToMavenLocal")) {
            System.console().printf "  [Disabled] Artifacts are not signed when published locally\n"
            false
        }
        else {
            def signingKey = findProperty("signingKey")
            def signingPassword = findProperty("signingPassword")
            useInMemoryPgpKeys(signingKey, signingPassword)
            true
        }
    }

    sign publishing.publications
}

publishing {
    publications {
        nebula(MavenPublication) {
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    licenses {
                        license {
                            name 'GNU Lesser General Public License, version 2.1'
                            url 'https://www.gnu.org/licenses/lgpl-2.1.html'
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }
}
